#!ipxe

# OpenBSD Operating System
# http://www.openbsd.org

:openbsd_menu
set os OpenBSD
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch amd64 ||
iseq ${os_arch} i386 && set os_arch i386 ||
menu ${os}
item 6.9 ${space} ${os} 6.9
item 6.8 ${space} ${os} 6.8
item 6.7 ${space} ${os} 6.7
item 6.6 ${space} ${os} 6.6
item 6.5 ${space} ${os} 6.5
item 6.4 ${space} ${os} 6.4
item snapshots ${space} ${os} 6.9 Latest Snapshot
choose ver || goto openbsd_exit
iseq ${ver} 6.9 && set image_ver 69 ||
iseq ${ver} 6.8 && set image_ver 68 ||
iseq ${ver} 6.7 && set image_ver 67 ||
iseq ${ver} 6.6 && set image_ver 66 ||
iseq ${ver} 6.5 && set image_ver 65 ||
iseq ${ver} 6.4 && set image_ver 64 ||
iseq ${ver} snapshots && set image_ver 69 ||
goto boot_openbsd

:boot_openbsd
iseq ${platform} pcbios && goto pcbios_boot ||
iseq ${platform} efi && goto efi_boot ||

:pcbios_boot
set src ${openbsd_mirror}/${openbsd_base_dir}/${ver}/${os_arch}/cd${image_ver}.iso
imgfree
initrd ${src}
chain ${memdisk} iso raw
goto openbsd_menu

:efi_boot
set src ${openbsd_mirror}/${openbsd_base_dir}/${ver}/${os_arch}/install${image_ver}.img
imgfree
sanboot ${src}
goto openbsd_menu

:openbsd_exit
clear menu
exit 0
